buildscript {
    repositories {
        mavenCentral() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
        gradlePluginPortal() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
    }

    dependencies {
        classpath 'com.palantir.jakartapackagealignment:jakarta-package-alignment:0.6.0'
        classpath 'com.palantir.baseline:gradle-baseline-java:5.40.0'
        classpath 'com.palantir.gradle.consistentversions:gradle-consistent-versions:2.20.0'
        classpath 'com.palantir.gradle.externalpublish:gradle-external-publish-plugin:1.15.0'
        classpath 'com.palantir.gradle.gitversion:gradle-git-version:3.0.0'
        classpath 'com.palantir.gradle.jdks:gradle-jdks:0.34.0'
        classpath 'com.palantir.gradle.jdkslatest:gradle-jdks-latest:0.12.0'
        classpath 'com.palantir.javaformat:gradle-palantir-java-format:2.40.0'
        classpath 'gradle.plugin.org.inferred:gradle-processors:3.7.0'
        classpath 'me.champeau.jmh:jmh-gradle-plugin:0.7.2'
    }
}
allprojects {
    apply plugin: 'com.palantir.jakarta-package-alignment'
}

apply plugin: 'java-library'
apply plugin: 'com.palantir.baseline'
apply plugin: 'com.palantir.baseline-java-versions'
apply plugin: 'com.palantir.consistent-versions'
apply plugin: 'com.palantir.external-publish'
apply plugin: 'com.palantir.git-version'
apply plugin: 'com.palantir.java-format'
apply plugin: 'com.palantir.jdks.latest'
apply plugin: 'me.champeau.jmh'
apply plugin: 'org.inferred.processors'

group 'com.palantir.ri'
version gitVersion()

apply plugin: 'com.palantir.external-publish-jar'

repositories {
    mavenCentral() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
}

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-annotations'
    implementation 'com.palantir.safe-logging:safe-logging'

    testImplementation 'com.fasterxml.jackson.core:jackson-databind'
    testImplementation 'net.jqwik:jqwik'
    testImplementation 'org.junit.jupiter:junit-jupiter'

    jmh 'org.openjdk.jmh:jmh-core'
    jmh 'org.openjdk.jmh:jmh-generator-annprocess'
}

configurations.testCompileClasspath {
    exclude module: 'junit'
}

tasks.withType(Test) {
    useJUnitPlatform {
        includeEngines 'jqwik', 'junit-jupiter'
    }
}

jmh {
    profilers = ['gc']
}

tasks.jmhCompileGeneratedClasses {
    options.annotationProcessorPath = configurations.errorprone
    options.errorprone.enabled = false
}

javaVersions {
    libraryTarget = 11
    runtime = 21
}
