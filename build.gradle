buildscript {
    repositories {
        jcenter()
        maven { url 'https://palantir.bintray.com/releases/' }
    }

    dependencies {
        classpath 'com.palantir.baseline:gradle-baseline-java:0.14.0'
    }
}

plugins {
    id 'com.jfrog.bintray' version '1.7.2'
    id 'com.palantir.git-version' version '0.5.2'

    id 'jacoco'
    id 'java'
    id 'maven-publish'
}

apply plugin: 'com.palantir.baseline-config'

apply plugin: 'com.palantir.baseline-checkstyle'
apply plugin: 'com.palantir.baseline-eclipse'
apply plugin: 'com.palantir.baseline-error-prone'
apply plugin: 'com.palantir.baseline-idea'

sourceCompatibility = 1.7

group 'com.palantir.ri'
version gitVersion()

repositories {
    jcenter()
    maven { url 'https://palantir.bintray.com/releases/' }
}

dependencies {
    compile "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"

    testCompile "junit:junit:$junitVersion"
    testCompile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"

    baseline "com.palantir.baseline:gradle-baseline-java-config:0.14.0@zip"
}


task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    destinationDir rootProject.file("${buildDir}/libs")
}

publishing {
    publications {
        bintray(MavenPublication) {
            from components.java
            artifact(sourceJar) {
                classifier 'sources'
            }
            artifact(javadocJar) {
                classifier 'javadoc'
            }
        }
    }
}

tasks.check.dependsOn(javadoc)

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'releases'
        name = 'resource-identifier'
        userOrg = 'palantir'
        licenses = ['Apache-2.0']
        publications = ['bintray']
    }
}

tasks.bintrayUpload.dependsOn { generatePomFileForBintrayPublication }
tasks.bintrayUpload.dependsOn { sourceJar }
tasks.bintrayUpload.dependsOn { build }
